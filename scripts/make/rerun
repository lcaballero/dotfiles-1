#!/usr/bin/perl

#=======================================================================================================================
# Documentation                                                                                                     {{{1
#=======================================================================================================================
=head1 NAME

 rerun

=head1 AUTHOR

 Kartik Shenoy

=head1 SYNOPSIS

 rerun [OPTIONS]

=head1 DESCRIPTION

 Reruns the previous simulation according to the run_job command present
 in sim.out or summary.rj file if no arguments are specified

=head1 OPTIONS

  -a, --add       Add any options to the CMD. Can be specified multiple times
                  Overrides --remove
  -b, --build     Build model before running
  -c, --clean     Clean model and rebuild before running
                  Implies --build
      --cmd       Provide the cmd directly to clean up and run.
                  Overrides --file
  -f, --file      Name of file to pick up the run_job command from
                  Defaults to ./sim.out or ./summary.rj
      --lsf       Use LSF. LSF is not used by default
      --nolsf     Do not use LSF
  -m, --model     Use the specified model instead of the one obtained from the run_job command
  -r, --remove    Remove any options from the CMD. Can be specified multiple times
  -t, --test      Test run. Just print the command to execute but don't run it
  -v, --verbose   Verbose mode. Print values of all arguments
  -w, --wtf       Run WhyTheFail upon build failure (Enabled by default)
      --nowtf     Do not run WhyTheFail if build fails
  -y, --yaml      Enable logging via amdmsg_config.yaml (Enabled by default).
                  The yaml file to be used can be specified as an optional argument.
                  Defaults to /proj/aspen_func_dev0/ASPEN/kshenoy/amdmsg_config.yaml
      --noyaml    Disable logging (Delete amdmsg_config.yaml file)

=cut
# }}}1

use strict;
#use diagnostics;
use Getopt::Long;
use Pod::Usage;

Getopt::Long::Configure("bundling");
#Getopt::Long::Configure("pass_through");

# Default values for options
my %opts = (
  'add'     => "",
  'build'   => 0,
  'clean'   => 0,
  'cmd'     => "",
  'file'    => "sim.out",
  'lsf'     => 0,
  'model'   => "",
  'noyaml'  => 0,
  'remove'  => "",
  'test'    => 0,
  'verbose' => 0,
  'wtf'     => 1,
  'yaml'    => "/proj/aspen_func_dev0/ASPEN/kshenoy/amdmsg_config.yaml",
);
$opts{'file'} = "summary.rj" unless ( -e $opts{'file'} );

GetOptions(
  \%opts,
  'add|a=s',
  'build|b',
  'clean|c'     => sub { $opts{'build'} ||= $opts{$_[0]} = $_[1]; },
  'cmd=s',
  'file|f:s',
  'help|h|?'    => sub { pod2usage( -verbose => 1, -exitval => 0 ); },
  'lsf!',
  'model|m=s',
  'noyaml'      => sub { $opts{'yaml'} = "" },
  'remove|r=s',
  'test|t',
  'verbose|v!',
  'wtf|w!',
  'y|yaml:s',
) or pod2usage( -verbose => 1 ) && exit;

print "INFO: Ignoring extra arguments '@ARGV'\n" if ( $#ARGV >= 0 );

# Dump the input arguments
if ( $opts{'verbose'} ){
  print "add    : $opts{'add'}\n";
  print "build  : $opts{'build'}\n";
  print "clean  : $opts{'clean'}\n";
  print "cmd    : $opts{'cmd'}\n"   unless ( $opts{'cmd'}   eq "" );
  print "file   : $opts{'file'}\n"  if     ( $opts{'cmd'}   eq "" );
  print "lsf    : $opts{'lsf'}\n";
  print "model  : $opts{'model'}\n" if     ( $opts{'model'} ne "" );
  print "remove : $opts{'remove'}\n";
  print "test   : $opts{'test'}\n";
  print "yaml   : $opts{'yaml'}\n";
  print "\n";
}

# Get the run_job command from the file
if ( $opts{'cmd'} eq "" ){
  print "Command not specified. Obtaining it from $opts{'file'}...\n" if $opts{'verbose'};
  ( -e $opts{'file'} ) or pod2usage({ -verbose => 1, -message => "ERROR: File not found: $opts{'file'}\n"});
  # Stop searching after 1st match
  $opts{'cmd'} = `\\grep -s -m 1 run_job $opts{'file'}`;
  ( $opts{'cmd'} eq "" ) and pod2usage({ -verbose => 1, -message => "ERROR: Unable to locate the run_job command in $opts{'file'}\n" });
}
chomp( $opts{'cmd'} );

# Get the model from the run_job command
if ( $opts{'model'} eq "" ) {
  print "Model not specified. Obtaining it from the run_job command...\n" if $opts{'verbose'};
  ( $opts{'model'} ) = $opts{'cmd'} =~ /-model\s*(\w+)/;
}
print "Model: '$opts{'model'}'\n\n" if $opts{'verbose'};
$opts{'cmd'} =~ s/(-model)\s*\w+/$1 $opts{'model'}/;

# Include/Exclude the amdmsg_config.yaml file
if ( $opts{'yaml'} ne "" ){
  print  "Copying amdmsg_config.yaml...\n";
  system '\cp ' . $opts{'yaml'} . ' .' unless $opts{'test'};
} else {
  print  "Deleting amdmsg_config.yaml...\n";
  system '\rm amdmsg_config.yaml 2> /dev/null' unless $opts{'test'};
}
print "\n";

# Remove junk arguments
$opts{'cmd'} =~ s/-batch_name\s*\S+\s*//;
$opts{'cmd'} =~ s/-rtp_datafile\s*\S+\s*//;
$opts{'cmd'} =~ s/-p crdb\s*//g;
$opts{'cmd'} =~ s/-(?:no)?crdb\s*//g;
$opts{'cmd'} =~ s/-o ""\s*//;
$opts{'cmd'} =~ s/-(?:no)?delete//;
$opts{'cmd'} =~ s/-gzip\s*//;
$opts{'cmd'} =~ s/-test_id\s*\S+\s*//;

my $cmd_build  = "build -clean -model $opts{'model'} && " if $opts{'clean'};
   $cmd_build .= "build        -model $opts{'model'}"     if $opts{'build'};
   $cmd_build .= " || WhyTheFail"                         if $opts{'wtf'};

# Beautify :/
$cmd_build =~ s/^\s*|\s*$//g;
$cmd_build =~ s/\s+/ /g;
print "  $cmd_build\n" if ( $opts{'clean'} || $opts{'build'} );

# Add/Remove options to the end of the CMD
$opts{'cmd'} .= " -lsf" if $opts{'lsf'};
if ( $opts{'remove'} ne "" ){
  print "Removing option: '$opts{'remove'}'\n";
  $opts{'cmd'} =~ s/$opts{'remove'}//;
}
if ( $opts{'add'} ne "" ){
  print "Adding option: '$opts{'add'}'\n";
  $opts{'cmd'} .= " " . $opts{'add'};
}

# Beautify :/
$opts{'cmd'} =~ s/^\s*|\s*$//g;
$opts{'cmd'} =~ s/\s+/ /g;
print "  $opts{'cmd'}\n\n";

# Execute
unless ( $opts{'test'} ) {
  unless (  $opts{'build'}
         && system qq($cmd_build)
         ) {
    # Execute the run_job command if
    # * We don't have to build
    # * Build completed successfully
    exec qq($opts{'cmd'});
  }
}
