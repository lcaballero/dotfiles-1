#!/usr/bin/perl

## Pragmas
use strict;
use diagnostics;
use warnings;
use Getopt::Long;
use Date::Calc;

use lib "/user/a0393831/lib";
use Spreadsheet::WriteExcel;
use Spreadsheet::WriteExcel::Utility;
use Parse::RecDescent;

use constant DEBUG => 0;
use constant ROW_START => 3;


## Command-line parameter handling
GetOptions(
	"ifile|i=s" => \my $ifile,
	"ofile|o=s" => \my $ofile,
	"verbose|v:i" => \my $v,
    "fields|f=i" => \my $fields,
);


## Variable Initialisation
$v|=DEBUG;

$ifile||=$ARGV[0];
defined($ifile) or die("ERROR: Input file not specified.\n");
die("ERROR : $ifile not found.\n") unless(-e $ifile);
open(my $fin,"<$ifile") or die("ERROR: Unable to open $ifile for reading.\n");

$ofile||=$ARGV[1];
unless(defined $ofile){
    ($ofile) = ($ifile =~ /(.*?)(?:(?:\.xls)\.csv)?$/);
    $ofile.=".xls";
    warn("INFO: Output file not specified. Writing the output to $ofile\n");
}
our $workbook = Spreadsheet::WriteExcel->new($ofile) or die("ERROR: Unable to open $ofile for writing.\n");

my $col_start=!(defined $fields);
$fields||=0;
my $offset=$col_start+$fields-1;

my %worksheet;
$worksheet{1}=$workbook->add_worksheet("Data");

my @arr_num_flag;
my $col_last;
my $format;

my $row=ROW_START;
my $col=$col_start;
while(<$fin>){
    chomp; s/,\s*,*$//;
    next if /^\s*$/;
    $col=$col_start;

    my @arr=split(/,/);
    foreach(0..$#arr){
        $format=$workbook->add_format();

        if($.==1){
            $col_last=$col_start+$#arr;
            $format->set_bold();
            $format->set_top(2);
            $format->set_bottom(2);
            $arr[$_]="\u$arr[$_]" if($col<=$offset);
        }

        $format->set_left(2) if($_==0);
        $format->set_right(($col==$col_last or $col==$offset) ? 2 : 1);
        
        if($arr[$_]=~/\b-?\d+(?:\.\d+)?(?:e[\+-]\d+)?\b/){
            if(abs($arr[$_])>=1 or $arr[$_]==0){
                $format->set_num_format('0.00') if($arr[$_]-int($arr[$_])!=0);
            }elsif(abs($arr[$_])>=1e-3){
                $format->set_num_format('0.000');
            }elsif(abs($arr[$_])<1e-3){
                $format->set_num_format('##0.00E+00');
            }
        }

        $worksheet{1}->write($row,$col,$arr[$_],$format);
        $col++;
    }
    $row++;
}
my $row_last=$row-1;

$col=$col_start;
$format=$workbook->add_format(top=>'2');
while($col<=$col_last){
    $worksheet{1}->write($row,$col,undef,$format);
    $col++;
}

$format=$workbook->add_format(bold=>'1',left=>'2',right=>'2',top=>'2');
$worksheet{1}->write(1,$offset,"Max",$format);
$format=$workbook->add_format(bold=>'1',left=>'2',right=>'2',bottom=>'2');
$worksheet{1}->write(2,$offset,"Min",$format);

my $format1;
my $format2;
for($col=$offset+1;$col<=$col_last;$col++){
    $format1=$workbook->add_format(top=>'2',num_format=>'##0.00E+00');
        $format1->set_right($col==$col_last ? 2 : 1);
    $format2=$workbook->add_format(bottom=>'2',num_format=>'##0.00E+00');
        $format2->set_right($col==$col_last ? 2 : 1);
    my $xy1=&xl_rowcol_to_cell(ROW_START+1,$col);
    my $xy2=&xl_rowcol_to_cell($row_last,$col);
    $worksheet{1}->write(1,$col,"=SUBTOTAL(4,$xy1:$xy2)",$format1);
    $worksheet{1}->write(2,$col,"=SUBTOTAL(5,$xy1:$xy2)",$format2);
}

$worksheet{1}->autofilter(ROW_START,$col_start,ROW_START,$col_last);
$worksheet{1}->freeze_panes(ROW_START+1,$col_start);

close $fin;
$workbook->close();
exit 0;
