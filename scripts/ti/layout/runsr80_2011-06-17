#!/bin/tcsh -f
#==================================================================================================#
#                                                                                                  #
# DESCRIPTION:                                                                                     #
#   Script to perform DRC and LVS checks                                                           #
#                                                                                                  #
# SYNTAX:                                                                                          #
#   runsr80 <libname> <cellname> [drc/lvs]                                                         #
#                                                                                                  #
# REQUIRED:                                                                                        #
#                                                                                                  #
# CHANGELOG:                                                                                       #
#   Created by Naga (a0393510)                                                                     #
#                                                                                                  #
#   2011-06-17: Kartik Shenoy (a0393831)                                                           #
#     - Replaced DRC commands with the one supplied by Ajith                                       #
#                                                                                                  #
#==================================================================================================#

set wkarea=/proj/gateway10/products/sr80/expts/a0393831/layout_checks
set laffs_dir=${wkarea}/laffs_dir
set checkers_dir=${wkarea}/checkers_dir

setenv SR80TECH /db/coremac1/products/c28.p/tech/
setenv HERCULES_HOME /apps/synopsys/hercules/2008.09-SP2/

set runlvs = "yes"
set rundrc = "yes"

if ($#argv < 2)  then
    echo "Usage: runsr80 <libname> <cellname> [drc/lvs]"
    exit
else if ($#argv == 3) then
    if($argv[3] == drc) then
        set runlvs  = "no"
        echo "  INFO  : Will run DRC only"
    else if ($argv[3] == lvs) then
        set rundrc = "no"
        echo "  INFO  : Will run LVS only"
    endif
endif

set lib  = $1
set cellname = $2



#####################################################################
### Convert the Cadence Database to .gds
#####################################################################
mkdir -p $laffs_dir
/apps/ame/bin/strmout \
    -library $lib \
    -strmFile $laffs_dir/$cellname.gds \
    -runDir ./ \
    -topCell $cellname \
    -view layout \
    -techLib tsmcN28 \
    -layerMap /apps/pvkit/C28Pphyverlib/v1.0.4/layerMap/c28p/cds2gds.map



#####################################################################
### Run Hercules DRC on the .gds file
#####################################################################
if($rundrc == yes) then
    mkdir -p $checkers_dir/$cellname/drc
    set gdsname = "$laffs_dir/${cellname}.gds"
    set tool = "icv"
    
    /apps/ame/bin/plato drc \
        -technology c28p \
        -metal 10m_5x2y2r \
        -input_path $gdsname  \
        -input_cell $cellname \
        -runset basic \
        -tool $tool \
        -dir $checkers_dir/$cellname/drc \
        -config /proj/gateway/layout/sr80/scripts/c28p_10m_5x2y2r_drc_basic_icv_native_settings.cfg

endif



#####################################################################
### Convert the Cadence Database to CDL Netlist
#####################################################################
if($runlvs == yes) then
    \cp -rf /proj/gateway/layout/sr80/scripts/starxt/DFM/ $checkers_dir/$cellname/
    \cp -rf /proj/gateway/layout/sr80/scripts/starxt/unit.cdl $checkers_dir/$cellname/
    \cp -rf /proj/gateway/layout/sr80/scripts/starxt/hercules_deck $checkers_dir/$cellname/

    /proj/coremac/bin/cadence_scripts/cds2cdlnetlist $lib $cellname schematic $cellname.cdl
    cat $cellname.cdl /cdb/acd/sr80/sr80_io/cds/rupoly.cdl > $checkers_dir/$cellname/cellname.cdl

    $HERCULES_HOME/bin/AMD.64/hercules -i $wd/$cellname.gds -b $cellname $checkers_dir/$cellname/hercules_deck

endif



## DRC Report
if($rundrc == yes) then
    echo "Generated on: `date +'%b %d %H:%M:%S %Y'`" > $checkers_dir/$cellname/drc/$$
    echo >> $checkers_dir/$cellname/drc/$$
    echo >> $checkers_dir/$cellname/drc/$$
    cat $checkers_dir/$cellname/drc/$cellname.LAYOUT_ERRORS >> $checkers_dir/$cellname/drc/$$
    \mv -f $checkers_dir/$cellname/drc/$$ $checkers_dir/$cellname/drc/$cellname.LAYOUT_ERRORS
    echo "  DRC report: $checkers_dir/$cellname/drc/$cellname.LAYOUT_ERRORS"
endif
## LVS Report
if($runlvs == yes) then
    echo "  LVS report: $checkers_dir/$cellname/lvs/"
endif
